# SPDX-License-Identifier: MIT

# Configure package file for this CMake package.
#
# Find this package with find_dependency(<pkg> CONFIG REQUIRED).
#
# This package requires CMake >= 3.17 (we may want message(CHECK_*) at some
# point).

# If this module has a dependency, this other module defines the
# find_dependency() macro for that purpose.
include(CMakeFindDependencyMacro)

# Internal function; do not use.
#
# @param tgt The target whose sources to run clang-format for.
#
# @param format_check_tgt The target that will run the clang-format check for
# the specified target's sources.
#
# @param command_or_empty The specified clang-format command to run. A few
# flags will be appended on top of them. If empty, then no command is added is
# done.
#
# @param check_main_target The target to add the created check target as a
# dependency of. Helpful to be able to run clang-format checks on all targets
# of the CMake build.
#
# @param build_depends Whether building the target "tgt" should depend on the
# check being done.
function(__ade_clang_format_check_target
	tgt
	format_check_tgt
	command_or_empty
	check_main_target
	build_depends)
  get_target_property(tgt_sources "${tgt}" SOURCES)
  get_target_property(tgt_source_dir "${tgt}" SOURCE_DIR)

  message(VERBOSE "Creating check target ${format_check_tgt}.")
  if(NOT "${tgt_sources}" STREQUAL "" AND
	  NOT "${command_or_empty}" STREQUAL "")
	add_custom_target("${format_check_tgt}"
	  COMMENT "Running clang-format check for target ${tgt}..."
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND ${command_or_empty} -n --Werror ${tgt_sources})
  elseif("${tgt_sources}" STREQUAL "")
	# This case has to be handled separately, since running clang-format with
	# no sources causes clang-format to take the source through stdin.
	add_custom_target("${format_check_tgt}"
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND echo "Not running clang-format check for target ${tgt}: No sources")
  else()
	# If no command is specified, then this target should do nothing. The
	# reasons for this choice:
	#
	# 1) To avoid users of this CMake package having to do something like
	# "If we're one the CI machine, then call ade_clang_format, otherwise do
	# nothing." This sort of thing would be messy and error-prone, because it
	# forces having specific configurations for the CI machine inside the
	# CMakeLists.txt, which should generally remain declarative and focus
	# mostly just on describing project structure and install package
	# structure. Instead, CI configs and dev configs for the CMake project
	# should be controlled externally to the CMake project, e.g. via the
	# CMakePresets.json file, via some CMake cache variable specified on the
	# commandline, or via an environment variable. To be fair, if we do not
	# need to call clang-format (e.g. non-dev, non-CI environment), then the
	# calling CMakeLists.txt might try not to attempt finding this package so
	# that these setups don't need this package installed. But still, we should
	# not make that decision here. Let's leave that decision to whoever uses
	# us; they know their setup better than we do.
	#
	# 2) To remain consistent with CMake's clang-tidy integration, where if you
	# don't define the CMAKE_<lang>_CLANG_TIDY variable nor the
	# <lang>_CLANG_TIDY target property, then clang-tidy is not run.
	add_custom_target("${format_check_tgt}"
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND echo "Not running clang-format check for target ${tgt}: No command specified.")
  endif()

  if(NOT "${check_main_target}" STREQUAL "")
	if (NOT TARGET "${check_main_target}")
	  message(VERBOSE "Creating check target ${check_main_target} and adding ${format_check_tgt} as a dependency.")
	  add_custom_target("${check_main_target}"
		COMMENT "Running clang-format check sub-targets."
		WORKING_DIRECTORY "${tgt_source_dir}")
	else()
	  	  message(VERBOSE "Adding ${format_check_tgt} as a dependency of ${check_main_target}.")
	endif()
	add_dependencies("${check_main_target}" "${format_check_tgt}")
  endif()

  if(build_depends)
	message(VERBOSE "Adding ${format_check_tgt} as a dependency of ${tgt}.")
	add_dependencies("${tgt}" "${format_check_tgt}")
  endif()
endfunction()

# Internal function; do not use.
#
# @param tgt The target whose sources to run clang-format for.
#
# @param format_fix_tgt The target that will run clang-format for the
# specified target.
#
# @param command_or_empty The specified clang-format command to run. A few
# flags will be appended on top of them. If empty, then the target is created
# but is a no-op.
#
# @param fix_main_target The target to add the created check target as a
# dependency of. Helpful to be able to run clang-format checks on all targets
# of the CMake build.
function(__ade_clang_format_fix_target
	tgt
	format_fix_tgt
	command_or_empty
	fix_main_target)
  if(TARGET "${format_fix_tgt}")
	message(FATAL_ERROR
	  "Cannot create fix target \"${format_fix_tgt}\" for clang-format: A "
	  "target with the same name already exists. Use the "
	  "POSTFIX keyword of ade_clang_format_targets() to specify a different "
	  "target.")
  endif()

  get_target_property(tgt_sources "${tgt}" SOURCES)
  get_target_property(tgt_source_dir "${tgt}" SOURCE_DIR)

  message(VERBOSE "Creating fix target ${format_fix_tgt}.")
  if(NOT "${tgt_sources}" STREQUAL "" AND
	  NOT "${command_or_empty}" STREQUAL "")
	add_custom_target("${format_fix_tgt}"
	  COMMENT "Running clang-format fix for target ${tgt}..."
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND ${command_or_empty} -i ${tgt_sources})
  elseif("${tgt_sources}" STREQUAL "")
	# This case has to be handled separately, since running clang-format with
	# no sources causes clang-format to take the source through stdin.
	add_custom_target("${format_fix_tgt}"
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND echo "Not running clang-format fix for target ${tgt}: No sources")
  else()
	# If no command is specified, then this target should do nothing, for the
	# same reasons as above.
	add_custom_target("${format_fix_tgt}"
	  WORKING_DIRECTORY "${tgt_source_dir}"
	  COMMAND echo "Not running clang-format fix for target ${tgt}: No command specified.")
  endif()

  if(NOT TARGET "${fix_main_target}")
	add_custom_target("${fix_main_target}"
	  COMMENT "Running clang-format fix sub-targets."
	  WORKING_DIRECTORY "${tgt_source_dir}")
	message(VERBOSE "Creating main fix target ${fix_main_target} and adding ${format_fix_tgt} as a dependency.")
  else()
	message(VERBOSE "Adding ${format_fix_tgt} as a dependency of ${fix_main_target}.")
  endif()
  add_dependencies("${fix_main_target}" "${format_fix_tgt}")
endfunction()

function(ade_clang_format_targets)
  message(VERBOSE "ade_clang_format_targets(): Parsing arguments.")
  ####
  #
  # TL;DR: This is an explanation on how to pass arguments to CMake functions.
  # For literal strings, if they don't contain whitespaces or
  # semicolons, then you *may* use the my_string syntax, otherwise use the
  # "my_string" syntax. For variable expansion, if you want to expand a
  # variable containing a list into zero or more function arguments, then do
  # the variable expansion outside of any quotes ${like_this}. Otherwise, do
  # the variable expansion inside quotes "${like_this}".
  #
  ####
  #
  # Most of this CMake package needs to handle variables and strings that may
  # have whitespaces, as well as lists. To explain how supporting that works,
  # let's have a FULL explanation of CMake lists, strings and variable
  # expansion.
  #
  # According to experimentation, there are 3 cases:
  #
  # 1) Quoted strings ("like \t \n ; this") are ALWAYS considered a single
  # unmodified argument, whether the string contains whitespaces, semicolons
  # or is an empty string. If a variable is expanded inside quotes
  # ("${like_this}"), the expansion is literally replaced by the contents of
  # the variable to produce the string, even if the variable is a list. By the
  # way, in CMake a list is a semicolon-separated string. So:
  #     set(my_list a " b " "c ;d")
  # ...produces a list variable of four-elements: "a; b ;c ;d".
  #
  # 2) Unquoted string literal: Begins and ends in first non-whitespace,
  # non-semicolon character. E.g.:
  #     HELLO  \t\n  WORLD;! ; \ \; ; ?;;;;
  # ...is considered to be five arguments, the same as if we had written:
  #     "HELLO" "WORLD" "!" " ;" "?"
  # Because this is really weird, for clarity and to avoid bugs it's advisable
  # to quote literal strings unless what happens is obvious.
  #
  # 3) Unquoted variable expansion: This is the variable expansion to use if
  # you want to expand a list in a situation where you want one argument per list
  # item. E.g.:
  #     set(my_list qwer "as  df ;  z\n xc\tv")
  # Any unquoted variable expansion is considered to be a special situation
  # where the variable is considered to be a list, with each item separated by
  # a semicolon. Therefore this:
  #     ${my_list}
  # ...expands to three arguments, as if we had written:
  #     "qwer" "as  df " "  z\n xc\tv"
  #
  # WARNING: You *cannot* always use unquoted variable expansion. For example:
  #     if(${my_list} STREQUAL "")
  # ...when my_list is empty will expand to:
  #     if(STREQUAL "")
  # ...which will cause a CMake error.

  # For a quick explanation of cmake_parse_arguments(), see:
  # https://youtu.be/IZXNsim9TWI?t=1618
  cmake_parse_arguments(
	parsed                           # Magic var prefix

	# Here we want an actual single argument containing semicolons, since this
	# is what cmake_parse_arguments() expects as this argument.
	"BUILD_DEPENDS;NO_BUILD_DEPENDS" # Options

	# Same here
	"FIX_MAIN_TARGET;FIX_POSTFIX;CHECK_MAIN_TARGET;CHECK_POSTFIX" # Single-value keywords

	# Same here
	"TARGETS;COMMAND"                # Multi-value keywords

	# Here we want to pass one argument per list item of ARGN, which is the
	# list of arguments passed to *this* function. So this variable expansion
	# must not be inside a quoted string.
	${ARGN})

  if(parsed_UNPARSED_ARGUMENTS)
	message(FATAL_ERROR
	  "Unrecognized argument(s): ${parsed_UNPARSED_ARGUMENTS}.")
  endif()

  if(NOT DEFINED parsed_TARGETS)
	message(FATAL_ERROR "TARGETS keyword missing.")
  endif()

  # Parse & sanitize BUILD_DEPENDS and NO_BUILD_DEPENDS
  if(parsed_NO_BUILD_DEPENDS OR parsed_BUILD_DEPENDS)
	if(parsed_NO_BUILD_DEPENDS AND parsed_BUILD_DEPENDS)
	  message(FATAL_ERROR
		"Both BUILD_DEPENDS and NO_BUILD_DEPENDS were specified to ade_clang_format_targets().")
	endif()
	if(parsed_NO_BUILD_DEPENDS)
	  set(BUILD_DEPENDS OFF)
	else()
	  set(BUILD_DEPENDS ON)
	endif()
  else()
	if(DEFINED ADE_CLANG_FORMAT_BUILD_DEPENDS AND NOT ADE_CLANG_FORMAT_BUILD_DEPENDS)
	  set(BUILD_DEPENDS OFF)
	else()
	  set(BUILD_DEPENDS ON)
	endif()
  endif()

  if(DEFINED parsed_CHECK_POSTFIX)
	set(CHECK_POSTFIX "${parsed_FIX_POSTFIX}")
  else()
	set(CHECK_POSTFIX "-formatcheck")
  endif()

  if(DEFINED parsed_CHECK_MAIN_TARGET)
	set(CHECK_MAIN_TARGET ${parsed_CHECK_MAIN_TARGET})
  elseif(DEFINED ADE_CLANG_FORMAT_MAIN_TARGET)
	set(CHECK_MAIN_TARGET ${ADE_CLANG_FORMAT_MAIN_TARGET})
  else()
	set(CHECK_MAIN_TARGET formatcheck)
  endif()

  if(DEFINED parsed_FIX_POSTFIX)
	set(FIX_POSTFIX "${parsed_FIX_POSTFIX}")
  else()
	set(FIX_POSTFIX "-formatfix")
  endif()

  if(DEFINED parsed_FIX_MAIN_TARGET)
	set(FIX_MAIN_TARGET ${parsed_FIX_MAIN_TARGET})
  elseif(DEFINED ADE_CLANG_FORMAT_MAIN_TARGET)
	set(FIX_MAIN_TARGET ${ADE_CLANG_FORMAT_MAIN_TARGET})
  else()
	set(FIX_MAIN_TARGET formatfix)
  endif()

  if(DEFINED parsed_COMMAND)
	set(COMMAND_OR_EMPTY ${parsed_COMMAND})
  elseif(DEFINED ADE_CLANG_FORMAT)
	set(COMMAND_OR_EMPTY ${ADE_CLANG_FORMAT})
  else()
	message(VERBOSE
	  "ade_clang_format_targets(): COMMAND keyword missing and "
	  "ADE_CLANG_FORMAT is undefined; created clang-format targets will be a "
	  "no-op.")

	set(COMMAND_OR_EMPTY)
  endif()

  foreach(tgt IN LISTS parsed_TARGETS)
	set(format_check_tgt "${tgt}${CHECK_POSTFIX}")
	set(format_fix_tgt "${tgt}${FIX_POSTFIX}")

	if(NOT TARGET "${tgt}")
	  message(FATAL_ERROR "Target \"${tgt}\" does not exist")
	endif()

	__ade_clang_format_check_target(
	  "${tgt}"
	  "${format_check_tgt}"
	  "${COMMAND_OR_EMPTY}"
	  "${CHECK_MAIN_TARGET}"
	  "${BUILD_DEPENDS}")

	__ade_clang_format_fix_target(
	  "${tgt}"
	  "${format_fix_tgt}"
	  "${COMMAND_OR_EMPTY}"
	  "${FIX_MAIN_TARGET}")
  endforeach()
endfunction()

